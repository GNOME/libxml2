# libxml2-config.cmake
# --------------------
#
# Libxml2 cmake module.
# THis module sets the following variables:
#
# ::
#
#   LIBXML2_INCLUDE_DIR       - Directory where libxml2 headers are located.
#   LIBXML2_INCLUDE_DIRS      - Directory where libxml2 headers are located.
#   LIBXML2_LIBRARIES         - xml2 libraries to link against.
#   LIBXML2_VERSION_MAJOR     - The major version of libxml2.
#   LIBXML2_VERSION_MINOR     - The minor version of libxml2.
#   LIBXML2_VERSION_PATCH     - The patch version of libxml2.
#   LIBXML2_VERSION_STRING    - version number as a string (ex: "2.3.4")
#   LIBXML2_MODULES           - whether libxml2 has dso support

include("${CMAKE_CURRENT_LIST_DIR}/libxml2-export.cmake")

@PACKAGE_INIT@

set(LIBXML2_VERSION_MAJOR  @LIBXML_MAJOR_VERSION@)
set(LIBXML2_VERSION_MINOR  @LIBXML_MINOR_VERSION@)
set(LIBXML2_VERSION_PATCH  @LIBXML_MICRO_VERSION@)
set(LIBXML2_VERSION_STRING "@VERSION@")
set(LIBXML2_INSTALL_PREFIX ${PACKAGE_PREFIX_DIR})
set(LIBXML2_LIBRARY_DIR    ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/libxml2)


include(CMakeFindDependencyMacro)

if(@LIBXML2_WITH_ICONV@)
  find_dependency(Iconv)
endif()

if(NOT @BUILD_SHARED_LIBS@)
  if(@LIBXML2_WITH_THREADS@)
    find_dependency(Threads)
  endif()

  if(@LIBXML2_WITH_ICU@)
    find_dependency(ICU)
  endif()

  if(@LIBXML2_WITH_LZMA@)
    find_dependency(LibLZMA)
  endif()

  if(@LIBXML2_WITH_ZLIB@)
    find_dependency(ZLIB)
  endif()
endif()

get_target_property(LIBXML2_INCLUDE_DIR LibXml2::LibXml2 INTERFACE_INCLUDE_DIRECTORIES)
set(LIBXML2_INCLUDE_DIRS ${LIBXML2_INCLUDE_DIR})
set(LIBXML2_LIBRARIES LibXml2::LibXml2)

# whether libxml2 has dso support
set(LIBXML2_MODULES @LIBXML2_WITH_MODULES@)

mark_as_advanced( LIBXML2_INCLUDE_DIRS LIBXML2_INCLUDE_DIR LIBXML2_LIBRARIES )
